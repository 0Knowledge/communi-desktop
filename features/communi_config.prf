######################################################################
# Communi
######################################################################

!verbose:CONFIG += silent

CONFIG(debug, debug|release) {
    OBJECTS_DIR = debug
    MOC_DIR = debug
    RCC_DIR = debug
    UI_DIR = debug
} else {
    OBJECTS_DIR = release
    MOC_DIR = release
    RCC_DIR = release
    UI_DIR = release
}

QT += network
greaterThan(QT_MAJOR_VERSION, 4) {
    QT += widgets
    DEFINES += QT_DISABLE_DEPRECATED_BEFORE=0x000000
} else {
    lessThan(QT_MAJOR_VERSION, 4) | lessThan(QT_MINOR_VERSION, 7) {
        error(Communi requires Qt 4.7 or newer but Qt $$[QT_VERSION] was detected.)
    }
}

isEmpty(QMAKE_EXTENSION_STATICLIB):QMAKE_EXTENSION_STATICLIB = lib

defineTest(communiAddLib) {
    LIBNAME = $$1
    isEmpty(LIBNAME):error(LIBNAME must be set)
    LIBS *= -L$$BUILD_ROOT/lib
    LIBS *= -l$$LIBNAME
    PRE_TARGETDEPS *= $$BUILD_ROOT/lib/$${QMAKE_PREFIX_STATICLIB}$${LIBNAME}.$${QMAKE_EXTENSION_STATICLIB}
    export(LIBS)
    export(PRE_TARGETDEPS)
}

defineTest(communiAddStaticPlugin) {
    PLUGINNAME = $$1
    isEmpty(PLUGINNAME):error(PLUGINNAME must be set)
    LIBS *= -L$$BUILD_ROOT/plugins
    LIBS *= -l$$PLUGINNAME
    PRE_TARGETDEPS *= $$BUILD_ROOT/plugins/$${QMAKE_PREFIX_STATICLIB}$${PLUGINNAME}plugin.$${QMAKE_EXTENSION_STATICLIB}
    export(LIBS)
    export(PRE_TARGETDEPS)
}
